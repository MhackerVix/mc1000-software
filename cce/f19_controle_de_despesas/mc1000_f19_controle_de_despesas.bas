1  REM >`C2 ~01>~E92!~01>~032"~01:~1B~01~FE~03`@>~F42!~01>~012D~03`I
2  CALL 986
5  REM **CONTROLE MENSAL**
6  REM **CCESOFT**
7 Y = 1000000
9  POKE 836,0
10  HOME 
15  GOSUB 2230
30  INVERSE : PRINT ~20 TAB( 2)"ENTRE COM OS SEGUINTES DADOS."
31  NORMAL 
32  PRINT 
35  INPUT "DIA....= ";DD
40  INPUT "MES....= ";MM
55  INPUT "ANO....= ";AA
60  INPUT "RENDA MENSAL = ";RM$
61  IF  VAL (RM$) = 0 GOTO 60
62 RM =  VAL (RM$)
65  PRINT : PRINT : INPUT "OS VALORES ESTAO CORRETOS (S/N)";A$: IF A$ = "N" GOTO 10
70  GOTO 347
75  HOME 
80  GOSUB 2230: PRINT 
90  PRINT  TAB( 3)"ALTERACAO--------------<A>"
95  PRINT  TAB( 3)"EXCLUSAO---------------<B>"
100  PRINT ~20~20 TAB( 3)"INCLUSAO---------------<C>"~20~20~20~20~20~20
105  PRINT  TAB( 3)"LISTAGEM POR PERIODO---<D>"
110  PRINT  TAB( 3)"LISTAGEM GERAL---------<E>"
115  PRINT  TAB( 3)"BAIXA------------------<F>"
120  PRINT  TAB( 3)"PROCURA----------------<G>"
125  PRINT  TAB( 3)"GRAVA ARQUIVO----------<H>"
126  PRINT  TAB( 3)"CARREGA ARQUIVO--------<I>"
127  PRINT 
130  INPUT "ENTRE COM A FUNCAO DESEJADA.";V$
135 P =  ASC (V$) - 64
140  ON ~20 ABS (P)~20 GOTO ~20400,850,720,930,1100,1320,1500,1700,4700
145  PRINT : PRINT "   AS FUNCOES SAO VALIDAS QUANDONO INTERVALO DE  A -> I."
150  GOSUB 2200
155  GOTO 75
347  DIM J$(15)
350  DIM B(50)
355  DIM P(15,50): DIM A(50): DIM V(50)
370  DIM W(4,50)
375  GOTO 75
400  HOME ~20
405 A$ = "**ALTERACAO**"
410  PRINT  TAB( 16 -  LEN (A$) / 2);A$
430  PRINT : PRINT  TAB( 5)"*MENU PARA ALTERACAO*": PRINT : PRINT 
435  PRINT  TAB( 5)"VALOR-------------<1>"
440  PRINT  TAB( 5)"VENCIMENTO--------<2>"
445  PRINT  TAB( 5)"N. DE PRESTACOES--<3>"
450  PRINT : INPUT "QUAL DADO SERA MUDADO ";Q
460  ON ~20Q~20 GOTO ~20470,550,620: IF Q > 3 GOTO 400
470  HOME 
471  GOSUB 2500
472  IF X = 1 GOTO 400
475  PRINT  TAB( 2)"NOME.......= ";: GOSUB 4200: PRINT 
480  PRINT  TAB( 2)"VALOR......= ";: IF V(CD) / Y >  = 1 GOTO 483
481  PRINT V(CD): PRINT : PRINT : GOTO 485
483  PRINT V(CD) / Y;"MILHOES": PRINT : PRINT 
485  PRINT  TAB( 2)"NOVO VALOR = ";
490  INPUT M: IF M <  = 0 OR M > 9999999 GOTO 490
500  GOSUB 2000
505  IF X = 1 GOTO 470
510 V(CD) = M:B(CD) = 1
515  PRINT : INVERSE : PRINT "        VALOR  ALTERADO         ": NORMAL 
520  INPUT "NOVA ALTERACAO  (S/N) ";A$
530  IF A$ = "S" GOTO 400
535  GOTO 75
550  HOME 
551  GOSUB 2500
552  IF X = 1 GOTO 550
555  PRINT  TAB( 2)"NOME........= ";: GOSUB 4200: PRINT 
560  PRINT  TAB( 2)"VENCIMENTO..= ";A(CD): PRINT : PRINT 
565  PRINT  TAB( 2)"NOVO VENCIMENTO = ";
566  INPUT MN: IF MN < 1 OR MN > 31 GOTO 565
570  GOSUB 2000
575  IF X = 1 GOTO 550
580 A(CD) = MN:B(CD) = 1
585  PRINT : INVERSE : PRINT "      VENCIMENTO  ALTERADO      ": NORMAL 
590  PRINT : INPUT "NOV~01 ALTERACAO (S/N) ";A$
595  IF A$ = "S" GOTO 400
600  GOTO 75
620  HOME 
621  GOSUB 2500
622  IF X = 1 GOTO 620
625  PRINT  TAB( 2)"NOME.........= ";: GOSUB 4200
630  PRINT  TAB( 2)"N. DE PREST..= ";: GOSUB 4300~20: PRINT : PRINT 
635  INPUT "NOVO N. DE PREST. = ";M$
640  GOSUB 2000
645  IF X = 1 GOTO 620
650  GOSUB 4100:B(CD) = 1: PRINT 
655  INVERSE : PRINT "     N. DE PREST. ALTERADA.     ": NORMAL : PRINT 
665  INPUT " NOVA ALTERACAO (S/N)";A$
670  IF A$ = "S" GOTO 400
675  GOTO 75~20
720 X = 0: HOME 
725  GOSUB 2500
730  IF ~20X = 1 GOTO 720
735  HOME 
736 A$ = "** INCLUSAO DE DADOS **"
737  PRINT  TAB( 16 -  LEN (A$) / 2);A$
739  PRINT 
740  INPUT "NOME........= ";M$
742  IF  LEN (M$) > 15 GOTO 740
745  GOSUB 4000
750  INPUT "VALOR.......= ";K$
752  IF  VAL (K$) = 0 GOTO 750
755 V(CD) =  VAL (K$)
760  INPUT "VENCIMENTO..= ";K$: IF  VAL (K$) = 0 GOTO 760
765 A(CD) =  VAL (K$)
770  INPUT "N.DE PREST..= ";M$~20~20~20~20~20
772  IF M$ <  > "CTE" AND  VAL (M$) = 0 OR  LEN (M$) > 3 GOTO 770
775  GOSUB 4100
785  GOSUB 2000
790  IF X = 1 GOTO 720
793 B(CD) = 1
796  INVERSE : PRINT "       VALORES  INCLUIDOS       ": NORMAL 
800  PRINT : INPUT "NOVA INCLUSAO (S/N)";A$
805  IF A$ = "S" GOTO 720
810  GOTO 75
850  HOME 
865 A$ = "** EXCLUSAO DE DADOS **"
870  PRINT  TAB( 16 -  LEN (A$) / 2);A$
875  GOSUB 2500
877  IF B(CD) = 0 GOTO 850
880  IF X = 1 GOTO 850
885  PRINT "NOME.....= ";: GOSUB 4200
886  PRINT 
890  GOSUB 2000
895  IF X = 1 GOTO 850
900  GOSUB 4600
905  PRINT : INVERSE : PRINT "       REGISTRO EXCLUIDO        ": NORMAL 
910  PRINT : INPUT "NOVA EXCLUSAO (S/N)";A$
915  IF A$ = "S" GOTO 850
920  GOTO 75
930  HOME 
950 XX = 0: GOSUB 3000
955  INPUT "INICIO...= ";IN
960  IF IN <  = 0 OR IN > 31 GOTO 980
963  PRINT 
965  INPUT "FIM......= ";FI
966  PRINT 
970  IF FI <  = 0 OR FI > 31 GOTO 980
975  GOTO 1000
980  HOME 
985  FOR T = 0 TO 6: PRINT : NEXT T
986  INVERSE : PRINT " OS VALORES  ESTAO ENTRE 1 E 31 ": NORMAL 
990  GOSUB 2200
992  HOME 
995  GOTO 955
1000  HOME 
1005  FOR CD = 1 TO 50
1010  IF B(CD) = 0 OR A(CD) < IN OR A(CD) > FI GOTO 1020
1015  GOTO 3110
1020  NEXT CD
1030  PRINT "TOTAL=> CR$";: IF XX / Y >  = 1 GOTO 1032
1031  PRINT XX: GOTO 1040
1032  PRINT XX / Y;"MILHOES"
1040  PRINT : INPUT "NOVA LISTAGEM POR PERIODO (S/N)";A$
1050  IF A$ = "S" GOTO 930
1060  GOTO 75
1100  HOME 
1120  GOSUB 3500
1125 XX = 0
1130  FOR CD = 1 TO 50
1135  IF B(CD) = 0 GOTO 1145
1140  GOTO 3110
1145  NEXT CD
1150  PRINT "TOTAL =>CR$";: IF XX / Y >  = 1 GOTO 1152
1151  PRINT XX: GOTO 1155
1152  PRINT XX / Y;"MILHOES"
1155  PR# 0
1159  PRINT 
1160  INPUT "  PRESSIONE <RETURN> PARA VOLTARAO MENU";U$
1170  GOTO 75
1320  HOME 
1321 A$ = "** BAIXA **": PRINT  TAB( 16 -  LEN (A$) / 2);A$: PRINT 
1325  GOSUB 2500
1327  IF B(CD) = 0 GOTO 1320
1330  IF ~20X = 1 GOTO 1320
1335  HOME : GOSUB 3250
1350  GOSUB 2000
1355  IF X = 1 GOTO 1320
1370  GOTO 4500
1385  PRINT : PRINT : INVERSE : PRINT "        BAIXA  EXECUTADA        "
1389  NORMAL 
1390  PRINT "       NOVA BAIXA. (S/N)?       "
1395  INPUT A$
1400  IF A$ = "S" GOTO 1320
1405  GOTO 75
1410  INVERSE : PRINT "     MENSALIDADE CONSTANTE.          NAO PODE SER EXCLUIDA.     ": NORMAL 
1425  GOSUB 2200
1430  GOTO 75~20~20~20~20~20~20~20~20~20~20
1440  GOSUB 4600
1445  INVERSE : PRINT "      MENSALIDADE EXCLUIDA.     ": PRINT "     NAO CONSTA MAIS DO ARQUIVO.": NORMAL 
1455  GOSUB 2200
1460  GOTO 1390
1500  HOME 
1506  PRINT : PRINT 
1510 A$ = "** PROCURA ARQUIVOS **"
1515  PRINT  TAB( 16 -  LEN (A$) / 2);A$
1516  PRINT : PRINT 
1520 WW = 0
1525  PRINT "    PARA PROCURAR ALGUM ARQUIVO,DIGITA-SE UMA LETRA OU UM CONJUNTO  DE  LETRAS  OU  ENTAO O NOMECOMPLETO DO ARQUIVO."
1530  PRINT "    DEPOIS PRESSIONE <RETURN>."
1535  INPUT K$
1536  HOME 
1540  IF ~20K$ = "" GOTO 1500
1545  PRINT : INVERSE : PRINT "           PROCURANDO           ": NORMAL 
1555  FOR CD = 1 TO 51
1557  IF CD = 51~20 GOTO 3700
1559  FOR J = 1 TO 15
1561 J$(J) =  CHR$ (P(J,CD))
1563  NEXT J
1565 X$ = ""
1567  FOR L = 1 TO 15
1569 X$ = X$ + J$(L)
1571  NEXT L
1573 X$ =  LEFT$ (X$, LEN (K$))~20~20
1575  IF X$ <  > K$ GOTO 1615
1580  PRINT "CODIGO......= ";CD
1585  PRINT "NOME........= ";: GOSUB 4200: PRINT 
1590  PRINT "VALOR.......= ";: IF V(CD) / Y >  = 1 GOTO 4805
1591  PRINT V(CD)
1595  PRINT "VENCIMENTO..= ";A(CD)
1600  PRINT "N. DE PREST.= ";: GOSUB 4300: PRINT 
1602  GOSUB 1800
1605 WW = WW + 1
1610  GOSUB 3800
1615  NEXT CD
1618  PRINT : INVERSE : PRINT "      NAO CONSTA MAIS NADA.     ": NORMAL 
1620  INVERSE : PRINT "  DESEJA OUTRA CONSULTA.(S/N) ? ": NORMAL 
1625  INPUT Q$
1630  IF Q$ = "S" GOTO 1500
1635  IF Q$ = "N" GOTO 75
1640  GOTO 1625
1700  HOME 
1725 A$ = "** GRAVACAO **"
1735  PRINT  TAB( 16 -  LEN (A$) / 2);A$
1740  PRINT : PRINT 
1745  INVERSE : PRINT "1) POSICIONE A FITA NO GRAVADOR 2) PRESSIONE <REC>  E  <PLAY> NO   GRAVADOR                     3) PRESSIONE <RETURN> NO MC-1000"
1746  NORMAL 
1755  INPUT Q$
1757  SAVE * B
1758  FOR FF = 1 TO 20: NEXT FF
1759  SAVE * P
1760  FOR FF = 1 TO 20: NEXT FF
1761  SAVE * A
1762  FOR FF = 1 TO 20: NEXT FF
1763  SAVE * V
1764  FOR FF = 1 TO 20: NEXT FF
1765  SAVE * W
1766  FOR FF = 1 TO 20: NEXT FF
1770  PRINT : INVERSE : PRINT "        PROGRAMA  SALVO.        ": NORMAL 
1775  GOTO 75
1800 AR = (V(CD) * 100) / RM
1805 RA =  INT (AR)
1810 SR = AR - RA
1815 SR = 100 * SR
1820 SR =  INT (SR)
1825 SR = SR / 100
1830 AR = RA + SR
1850  PRINT "EQUIVALE A ";AR; TAB( 17)" % DA SUA RENDA MENSAL"
1855  RETURN 
2000 X = 0: PRINT : PRINT 
2005  PRINT "OS DADOS ESTAO CORRETOS  (S/N)?"
2010  PRINT 
2020  INPUT A$
2025  IF A$ <  > "S" AND A$ <  > "N" GOTO 2020
2030  IF A$ = "S" THEN B(CD) = 1
2035  IF A$ = "N" THEN X = 1
2040  RETURN 
2200  FOR T = 0 TO 2000: NEXT T
2205  RETURN 
2230  PRINT  TAB( 7)"** CCE-SOFTWARE **"
2235  PRINT : PRINT "      CONTROLE DE DESPESAS": PRINT : PRINT 
2240  RETURN 
2500  INPUT "     CODIGO....=";Z$
2510  IF  VAL (Z$) = 0 GOTO 2500
2511 CD =  VAL (Z$)
2512  IF CD > 0 AND CD <  = 50 THEN X = 0
2515  IF CD = 0 THEN X = 1
2516  IF CD = 0 GOTO 2560
2517  IF CD > 50 GOTO 2800
2518  IF B(CD) = 0 AND P = 3 OR B(CD) = 0 AND P <  > 3 OR B(CD) = 1 AND P <  > 3 GOTO 2560
2520  PRINT : INVERSE : PRINT "        CODIGO INVALIDO.                  JA EXISTENTE.         ": NORMAL 
2530  GOSUB 2200
2535  HOME : GOTO 2500
2560  RETURN 
2800  HOME 
2810  PRINT : PRINT : PRINT 
2815  INVERSE : PRINT " CODIGO  MAIOR QUE O PERMITIDO. ": NORMAL 
2816  GOSUB 2200
2820  HOME : GOTO 75
3000  REM 
3010 A$ = "*LISTAGEM POR PERIODO*"
3015 B$ = "**********************"
3020  PRINT  TAB( 16 -  LEN (B$) / 2);B$
3025  PRINT  TAB( 16 -  LEN (A$) / 2);A$
3030  PRINT  TAB( 16 -  LEN (B$) / 2);B$
3035  PRINT : PRINT  TAB( 5)DD;"/";MM;"/";AA: PRINT : PRINT 
3040  RETURN 
3110 XX = XX + V(CD)
3120  PRINT "CODIGO......= ";CD
3125  PRINT "NOME........= ";: GOSUB 4200: PRINT 
3130  PRINT "VALOR.......= ";: IF V(CD) / Y >  = 1 GOTO 4800
3131  PRINT V(CD)
3135  PRINT "VENCIMENTO..= ";A(CD)
3140  PRINT "N. DE PREST.=";: GOSUB 4300: PRINT 
3145  GOSUB 1800: PRINT : PRINT 
3150  INPUT "APERTE <RETURN> PARA CONTINUAR.";U$: HOME 
3200  IF P = 4 GOTO 1020
3210  IF P = 5 GOTO 1145~20
3250 A$ = "** BAIXA **"
3255  PRINT  TAB( 16 -  LEN (A$) / 2);A$
3257  PRINT "CODIGO....= ";CD
3260  PRINT "NOME......= ";: GOSUB 4200
3261  PRINT 
3265  RETURN 
3500  REM 
3510 A$ = "*LISTAGEM GERAL*"
3515 B$ = "****************"
3520  PRINT  TAB( 16 -  LEN (B$) / 2);B$
3525  PRINT  TAB( 16 -  LEN (A$) / 2);A$
3530  PRINT  TAB( 16 -  LEN (B$) / 2);B$
3531  PRINT : PRINT  TAB( 8)DD;"/";MM;"/";AA: PRINT : PRINT 
3535  RETURN 
3700  HOME 
3705  TEMPO 150,2,1
3710  FOR ~20M = 1 TO 2
3715  SOUND 87,15,1
3720  SOUND 96,15,1
3725  SOUND 1,1,1
3726  NEXT M
3730  IF WW <  > 0 GOTO 1618
3735  INVERSE : PRINT "ESTE NOME NAO CONSTA DO ARQUIVO.": NORMAL 
3745  GOTO 1620
3800  PRINT : INVERSE : PRINT "  PRESSIONE <RETURN> PARA CONTI-NUAR.                           "
3801  NORMAL 
3810  INPUT O$
3815  HOME : INVERSE : PRINT "           PROCURANDO           ": NORMAL 
3820  RETURN 
4000 L$ = "               "
4005 M$ = M$ +  RIGHT$ (L$,15 -  LEN (M$))
4010  FOR I = 1 TO 15
4015 K$ =  MID$ (M$,I,1)
4020 P(I,CD) =  ASC (K$)
4025  NEXT I
4030  RETURN 
4100 L$ = "    "
4105 M$ = M$ +  RIGHT$ (L$,4 -  LEN (M$))
4110  FOR I = 1 TO 4
4115 K$ =  MID$ (M$,I,1)
4120 W(I,CD) =  ASC (K$)
4125  NEXT I
4130  RETURN 
4200  FOR K = 1 TO 15
4205  PRINT  TAB( 14 + K) CHR$ (P(K,CD));
4210  NEXT K
4220  RETURN 
4300  FOR K = 1 TO 4
4305  PRINT  TAB( 14 + K) CHR$ (W(K,CD));
4310  NEXT K
4315  RETURN 
4500 J$ =  CHR$ (W(1,CD)) +  CHR$ (W(2,CD)) +  CHR$ (W(3,CD)) +  CHR$ (W(4,CD))
4505  IF  VAL (J$) = 0 GOTO 1410
4510 XT =  VAL (J$)
4515 XT = XT - 1
4520  IF XT = 0 GOTO 1440
4525 M$ =  STR$ (XT)
4530  GOSUB 4100
4535  GOTO 1385
4600 B(CD) = 0:A(CD) = 0:V(CD) = 0
4605 M$ = "               "
4610  GOSUB 4000
4615 M$ = "    "
4620  GOSUB 4100
4625  RETURN 
4700  HOME : PRINT :A$ = "** CARREGA ARQUIVOS **": PRINT  TAB( 16 -  LEN (A$) / 2);A$: PRINT 
4715  PRINT : INVERSE : PRINT "1) POSICIONE A FITA NO GRAVADOR 2) PRESSIONE A TECLA <RETURN> NO   MC-1000                      3) PRESSIONE A TECLA  <PLAY>  DO   GRAVADOR                     "
4717  NORMAL 
4720  INPUT U$
4721  LOAD * B
4722  LOAD * P
4723  LOAD * A
4724  LOAD * V
4725  LOAD * W
4740  GOTO 75
4800  PRINT V(CD) / Y;"MILHOES": GOTO 3135
4805  PRINT V(CD) / Y;"MILHOES": GOTO 1595
